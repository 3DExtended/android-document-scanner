plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
    id 'signing'
}

android {
    compileSdk 32

    defaultConfig {
        minSdk 21
        targetSdk 32

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.quickbirdstudios:opencv:3.4.5'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = "io.github.3dextended"
            artifactId = "documentscanner"
            version = "1.3.6"

            afterEvaluate {
                from components.release

                pom {
                    name = 'Document Scanner'
                    description = 'Document Scanner'
                    url = 'https://github.com/3dextended/android-document-scanner'
                    licenses {
                        license {
                            name = 'The MIT License'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }
                    developers {
                        developer {
                            id = 'pesser'
                            name = 'Peter Esser'
                            email = 'me@peter-esser.de'
                        }
                    }
                    scm {
                        connection = 'scm:git@github.com:3dextended/android-document-scanner.git'
                        developerConnection = 'scm:git@github.com:3dextended/android-document-scanner.git'
                        url = 'https://github.com/3dextended/android-document-scanner'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            name = "sonatype" // was mavencentral
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            credentials {
                println findProperty("ossrhUsername")
                username findProperty("ossrhUsername")
                password findProperty("ossrhPassword")
            }
        }
    }
}

signing {
    //def signingKey = findProperty("ossrhSigningKey")
    //def signingPassword = findProperty("ossrhSigningPassword")
    //useInMemoryPgpKeys(signingKey, signingPassword)
    //sign publishing.publications.release
    useGpgCmd()
    sign publishing.publications.release
}